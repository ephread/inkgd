# author: Pat Scott
# title: â°
# description: Made for the Ludum Dare 41 Compo. The theme was "combine two incompatible genres." Thus, a textless text-based choose-your-own adventure.


VAR seenCat = false
VAR haveCoat = false
VAR time = 0

-> entre

=== increase_time ===

    ~ time ++
    â³

    -> DONE

=== entre ===

    = awaken
        +   (alarm) [â°]
        -
        +   ğŸ’¤
                <>{|ğŸ’¤|ğŸ’¤ğŸ’¤|ğŸ’¤ğŸ’¤ğŸ’¤}
                {alarm < 4: -> awaken | -> angry_cat ->}
        *   [ğŸ˜«]

        //Morning prep loop

        -   (prep) {not seenCat:ğŸˆ}
            {time == 3: <>ğŸ•–}

            *   (petCat) {not seenCat} [ğŸ‘‹]ğŸ˜½

            *   (fedCat) {seenCat} [ğŸ¥«]ğŸ˜¸ğŸ’©

            *   (cleanedShit) {fedCat} [ğŸ¥„]ğŸ˜¼

            *   (showered) {not dressed} ğŸš¿

            *   (ate) ğŸ¥

            *   (dressed) ğŸ‘”[]ğŸ‘–

        -   ~ seenCat = true
            <- increase_time
            {time < 4: -> prep}

        //End morning prep

        *   [ğŸ’]
        -
        *   [ğŸšª]
            * * {dressed} ğŸ§£ğŸ§¥
                ğŸšª
            * * ->
        -   -> street


    = angry_cat
        *   [ğŸ˜¾]
            ~ seenCat = true
        -
        *   [ğŸ¤—]ğŸ¤•
                <- increase_time
        ->->

    = street
        VAR coldness = 0
        ~ time = 0
        {awaken.dressed:
            ~ haveCoat = true

        }

        â„ï¸
        -> checkCold ->
        ğŸ‘£

        //Hobo encounter

        *   [ğŸ§”]ğŸ¤²
            * * (hobo_gaveCoat) {haveCoat} [ğŸ§¥]
                ~ haveCoat = false
                <- increase_time
                <>ğŸ™
            * * (hobo_gaveMoney) [ğŸ’¸]
                <- increase_time
                <>ğŸ™
            * * (hobo_fuckOff) [ğŸ–•]ğŸ’¨

        -   <- increase_time
            -> checkCold ->

        //Elder encounter

        *   [{~ğŸ‘µ|ğŸ‘´}]â™¿
            * * (elder_chat) [ğŸ’ª]
                <- increase_time
                <>ğŸ’¬
            * * (elder_wave) [ğŸ‘‹]
            * * (elder_horns) [ğŸ¤˜]ğŸ—¯ï¸

        -   <- increase_time
            -> checkCold ->

        //Getting on the train

        *   [ğŸš‡]ğŸ«
            * * (police) {stolenCoat} [ğŸ‘®]ï¸âœ‹
                ğŸ‘‰ğŸ§¥
                *** [ğŸ˜‡]ğŸ¤¥
                    ****[ğŸ‘ï¸]ğŸ‘Œ
                        <- increase_time
                *** [ğŸ˜ˆ]ğŸ’¨
                    ğŸ‘£
            * * ->

            - - ğŸšƒ
            * * (mugger) {not police} {time < 5} [ğŸ•µï¸]ğŸ”ª
                *** {not hobo_gaveMoney} {not boughtCoat} [ğŸ’¸]ğŸ–ï¸
                    ğŸƒ
                *** [ğŸ˜±]ğŸ•µğŸ‘ŠğŸ’¥
                    ğŸƒ
                    ğŸ¤•
                    <- increase_time
                *** [ğŸ˜¶]ğŸ”ª{~ğŸ‘§|ğŸ‘¦}
                        -> fight

            * * (on_train) ->

            - -
            <- increase_time
            <>
            <- increase_time


        -   -> adventure

    = checkCold
        {not haveCoat:
            ~ coldness ++
            <>ğŸŒ¡ï¸
        }
        *   {coldness > 1} [ğŸ¬]
            * * ğŸ§¥[]ğŸ”–
                *** (boughtCoat) {not street.hobo_gaveMoney} [ğŸ’¸]ğŸ§¥
                *** (stolenCoat) [ğŸ–]ğŸ—¯ï¸â—
                    ğŸ’¨

                --- <- increase_time
                    ~haveCoat = true
        +   ->

        -   ->->

    = fight
        *{hobo_gaveCoat}[ğŸ§”ğŸ§¥]ğŸ§”ğŸ’ª
            ğŸ§”ğŸ‘Š
            **[ğŸ•µ]ğŸ’¥
                ğŸ‘»
                ğŸ‘©ğŸ‘
                ğŸ§”ğŸ‘©ğŸ¤³
                -> on_train
        *[ğŸ˜£]
            **[ğŸ˜¡]ğŸ”¥
                ***[ğŸ™Œ]
                    ****[âœŠ]
                        ğŸ¤œ
                        ğŸ¤›
                        ğŸ‘Š
                        *****[ğŸ•µ]ğŸ’¥
                            ğŸ‘»
                            -> on_train
            **[ğŸ¤¡]
                ğŸ•µâ—â“
                ***[ğŸ’¨]
                    â„ï¸
                    -> checkCold ->
                    ğŸ‘£
                    <- increase_time
                    <>
                    <- increase_time
                    <>
                    <- increase_time
                    <>
                    <- increase_time

                    -> adventure


=== adventure ===

    = arrival_at_office

        VAR got_written_up = false

        *   [ğŸ¢]
            **  [ğŸ—£ï¸ğŸ“‹]
                {
                    - time < 5:
                        ğŸ•—
                    - time < 7:
                        ğŸ•£â—
                    - else:
                        ğŸ•˜â—â—
                            ~ got_written_up = true
                }

                {not entre.awaken.dressed:
                    ğŸ‘–â—
                    ~ got_written_up = true
                }

                {not entre.awaken.showered:
                    ğŸš¿â—
                    ~ got_written_up = true
                }

        -
        *   {got_written_up} [ğŸ“‹]âš ï¸ğŸ“œ
            ğŸ˜…
        *   ->

        -
        *   [ğŸ’»]ğŸ‘£
            ğŸ’»

        //Work loop

        VAR ok_we_get_it_you_write_a_lot = false
        VAR work_loop = 0

        -   (computer)
            ~ work_loop++

            *   {work_loop > 4} [ğŸ””]
                -> a_package

            *   {work_loop > 3} {not entre.awaken.cleanedShit} [ğŸ‘‚]ğŸ’
                -> cat_in_a_bag ->
                -> computer

            *   {work_loop > 2} {not entre.awaken.ate} [ğŸ‘ƒğŸ¥¡]ğŸš¶
                ğŸ¥¡
                **  [ğŸ–ï¸]ğŸ’¨
                    *** [ğŸ¥¢]ğŸ˜‹
                **  [ğŸ˜”]ğŸ’»
                --  -> computer

            *   {work_loop > 2} {boredom} [ğŸš½]ğŸš­
                --  (toilet)
                **  {smoke} [ğŸ’»]ğŸ‘£
                    -> computer
                ++  (squat) [ğŸ“±]{~ğŸ“‘|ğŸ®}
                    {squat > 3: -> shit}
                    <- increase_time
                    -> toilet
                **  (shit) [ğŸ’©]ğŸŒŠ
                    ğŸ‘£
                    -> computer
                **  (smoke) [ğŸš¬]ğŸ˜‰
                    -> toilet

            *   [ğŸ§]ğŸ”Š
                -> computer

            +   (write) {not ok_we_get_it_you_write_a_lot} [ğŸ“]
                {write > 3:
                    ğŸ—‘ï¸
                    <- increase_time
                    ~ ok_we_get_it_you_write_a_lot = true
                    -> computer
                }
                âœï¸
                {write > 1: ğŸ—‘ï¸}
                -> computer

            *   (boredom) [ğŸ“°]ğŸ‘€
                ğŸ˜´
                -> computer

        -

        -> finale

    = cat_in_a_bag
        *   {not entre.awaken.fedCat} [ğŸ˜¨]
            **  [ğŸ˜¾]ğŸ¤•
                ğŸ¾
        *   {entre.awaken.fedCat} [ğŸ˜²]ğŸ˜¿
            **  [ğŸ˜™]
                ğŸˆ
                ğŸ¾

        -   ->->

    = a_package
        *   [ğŸ“¦]ğŸ˜®

        -   (contents)
        *   [ğŸ„]ğŸ’«
            -> dive_in
        *   [ğŸ”]ğŸ’­
            -> contents

        -   (dive_in)
        *   [ğŸ˜µ]
            ğŸ¤­
            ğŸ‘¨â€ğŸ”§
            ğŸ‘©â€ğŸš€
            ğŸ‘½
            ğŸ‡

        -
        *   [ğŸŒŒ]ğŸŒ 
            ğŸŒˆ

        -
        +   (forest) ğŸŒ³[] ğŸ”ï¸ğŸï¸ ğŸŒ³ğŸŒ³ ğŸŒ³ğŸŒ´ ğŸŒ³ ğŸŒ´ ğŸŒ³ ğŸŒ´ğŸŒ´
            VAR have_crown = false

            ++  [ğŸ”ï¸]ğŸ‘£
                <-  increase_time
                -> mountain ->

            ++  {cat_in_a_bag} {not encounter} [ğŸ¾]
                <-  increase_time
                -> prints ->

            **  (rabbit) [ğŸŒ³]
                -> chase

            ++  [ğŸŒ´]ğŸ–ï¸ğŸŒŠğŸï¸
                +++ {not have_crown} [ğŸŒ]â™¨ï¸ğŸŒ¡ï¸
                    ğŸ‘£

                *** {have_crown} [ğŸŒ]ğŸŒŠ ğŸ‘¸ğŸ”±ğŸ‘¦ğŸŒŠ
                    ğŸ—¨ï¸ğŸ‘¸ğŸ‘‘

                    ****(ring) [ğŸ‘‘]ğŸŒŠ ğŸ‘¸ğŸ”±ğŸ¤´ğŸŒŠ
                        -> got_ring ->

                    ****[âŒ]ğŸŒŠ ğŸŒŠ

                    ----ğŸ‘£

        -   -> forest

    = mountain
        ğŸŒ‹ğŸ—» ğŸœï¸ ğŸ—»

        +   [ğŸŒ‹]
            <- increase_time
            --  (volcano) ğŸ”¥ğŸ”¥ğŸŒ‹ğŸ”¥ğŸ”¥
                {dragon > 1: ğŸ—¿}

            ++  (hole) {have_crown} {dragon < 2} [ğŸ•³ï¸]
                --- (dragon) {dragon < 3: ğŸ‰}

                *** [ğŸ˜]ğŸ—¨ï¸ğŸ²ğŸ‘‘
                    ****{have_crown} [ğŸ‘‘]
                        -> got_sword ->
                        -> volcano

                    ****(dont_give_crown) [âŒ]ğŸ”¥ğŸ”¥ğŸ”¥ğŸ²
                        ğŸ˜±ğŸ’¨
                        -> volcano

                *** {dont_give_crown} {have_crown} [ğŸ‘‘]
                        -> got_sword ->
                        -> volcano

                *** [ğŸ˜˜]ğŸ”¥ğŸ”¥ğŸ”¥ğŸ²
                    ğŸ˜…
                    -> hole

                +++ {not got_sword} [ğŸ˜±]{dragon > 1:ğŸ”¥ğŸ”¥ğŸ”¥}ğŸ’¨
                    -> volcano

                +++ ->
                    <>ğŸ’¨
                    -> volcano


            ++  [ğŸ”ï¸ï¸]
                <- increase_time

        +   [ğŸœï¸]
            <- increase_time
            <>ğŸ’¦
            {not desert:ğŸŒ™}
            --  (desert) ğŸŒµğŸŒµ ğŸŒµ ğŸŒµğŸŒµğŸŒµ
                {desert == 3:ğŸ¦‚ğŸ’¨}

            ++  {not have_crown} {not got_crown} [ğŸŒµ]{not saw_crown: ğŸ˜}
                --- (saw_crown) ğŸ‘‘

                *** (got_crown) [ğŸ–]ğŸŒŸ
                    ~ have_crown = true

                +++ [ğŸ”]ğŸ”¥<>
                    -> saw_crown

                +++ [â†©ï¸]


            **  [ğŸŒµ]ğŸ¦
                *** [ğŸ¦]ğŸ‘ï¸
                    ****[ğŸ¦]ğŸ‘ï¸â€ğŸ—¨ï¸
                        ğŸ¦ğŸ’¨


            **  [ğŸŒµ]ğŸ
                *** {entre.street.hobo_gaveMoney} [ğŸ§”ğŸ’¸]ğŸ§”ğŸ’ª
                    ****[ğŸ]ğŸ’¥
                        ğŸ‘»
                        ğŸ§”ğŸ’¨

                *** [ğŸ˜±]ğŸ’¥
                    ğŸ˜²
                    ****[ğŸ˜µ]ğŸ’¯
                        <- increase_time
                        <>
                        <- increase_time
                        <>
                        <- increase_time
                        ğŸ’¤
                        *****[ğŸ˜«]
                            -> a_package.forest

            ++  [ğŸ”ï¸]{not comet:â˜„ï¸}
                --- (comet)
                <- increase_time
                -> mountain

            --  -> desert


        +   [ğŸŒ³]
            -> leave

        -   -> mountain

        -   (leave) ğŸ‘£
            <- increase_time
            ->->

    = got_sword
        ğŸ—¡ï¸
        *   [ğŸ–ï¸]ğŸŒŸ
           ~ have_crown = false

        -   ->->

    = got_ring
        ğŸ’
        *   [ğŸ–ï¸]ğŸŒŸ
           ~ have_crown = false

        -   ->->

    = prints

        {flower > 2: ğŸŒ³}
        {bush: ğŸŒ¿}
        {rabbit > 3: ğŸŒ³}

        +   (flower) {flower < 3} [ğŸŒ³]
            ++  [ğŸŒ·]{flower < 3: ğŸ‘ƒ|ğŸŒº}

        *   (encounter) ğŸ¦‹[]ğŸŒ¿ğŸˆ
            **  (handsy) ï¸[ğŸ–ï¸]ğŸˆğŸ’¨

            **  (watchful) [ğŸ¤]ğŸ¦‹ğŸˆ
                ğŸ˜¸
                ğŸ’¨

        *   (bush) {encounter} [ğŸŒ¿]ğŸ—ï¸
            **  (got_key) [ğŸ–]ğŸŒŸ

            **  [ğŸ˜°]ğŸŒ¬ï¸ğŸ—ï¸ğŸ’¨

        +   {rabbit < 4} [ğŸŒ³]
            {not rabbit: ğŸ‡ğŸ’¨}

            ++  {rabbit} [ğŸŒ±]{rabbit < 3: ğŸƒ|ğŸ‚}

            ++  ->

            --  (rabbit)

        +   [â†©ï¸]
            -> leave

        -   -> prints

        -   (leave) ğŸ‘£
            <- increase_time
            ->->

    = chase
        *   [ğŸ‡]ğŸ°
            **  [ğŸ°]ğŸ°ğŸ’¬
               ğŸ‡ğŸ’¨

        -
        *   [ğŸ¾]ğŸ‘£

        -   ğŸŒ³ğŸŒŸğŸ‰âœ¨
        +   [ğŸ©]
        -   (tea_party)
            {tea_party < 2:ğŸ‡ğŸ’¨}
            {tea_party == 3 && encounter:ğŸˆğŸ¾}
            {tea_party < 4:âœ¨<>}
            {tea_party < 5:
                {used_crown:ğŸ¦„<>|ğŸ´<>}
            }
            ğŸµ
            {tea_party < 6:
                {used_crown:<>ğŸ’|<>ğŸ•º}
            }
            {tea_party < 4:<>âœ¨}


        -
        *   (used_crown) {have_crown} [ğŸ‘‘]âœ¨ğŸ©âœ¨
        *   (used_ring) {got_ring} {tea_party > 3} [ğŸ’]
            -> post_party

        +   {tea_party < 8} [ğŸµ]

        *   {tea_party > 7} [ğŸ˜µ]ğŸ’¯
            <- increase_time
            <>
            <- increase_time
            <>
            <- increase_time
            ğŸ’¤
            **[ğŸ˜«]
                -> post_party

        -   -> tea_party

        -   (post_party)
        +   [ğŸŒ³]ğŸ‘£
            {not used_ring: ğŸ’«}

        -   -> finale

=== finale ===

    //The mystery and the credits

    *   {entre.street.hobo_fuckOff} [ğŸ§”ğŸ–•]ğŸ§”ğŸ—¯ï¸
        **  (flip_off) [ğŸ–•]ğŸ”«ğŸ§”
            ***  {adventure.got_sword} [ğŸ—¡ï¸]ğŸ’ª
                ****[ğŸ§”]ğŸ’¥
                    ğŸ‘»

            ***  [ğŸ˜±]ğŸ’¥
                -> fin

        **  [ğŸ˜]ğŸ§”â—â“ {adventure.encounter:ğŸˆ|ğŸ‘}
            *** {adventure.encounter} [ğŸˆ]ğŸ˜¾
                ğŸ’¥
                ğŸ§”ğŸ’¨

            *** [ğŸ§¥]ğŸ¤

            *** [ğŸ–•]
                -> flip_off
    *   ->

    -   (box) ğŸ“¦ {adventure.encounter && not cat: ğŸˆ}
    *   [ğŸ”]ğŸ”’
    *   ->

    -
    *   {adventure.got_key} [ğŸ—ï¸]ğŸ”“
        **  [ğŸ“¦]ğŸŒŒ
            *** [ğŸ˜µ]ğŸ’«

    *   (cat) {adventure.encounter} [ğŸˆ]ğŸ™€
        ğŸ¾
        -> box

    *   [ğŸ¤œ]ğŸ’¥
        **  [ğŸ¤›]ğŸ’¥
            *** [ğŸ‘Š]ğŸ’¥
                ****ğŸ“¦[]ğŸ”’
                    ğŸ˜«
                    *****[âœŒï¸]

    -   (fin)
    *   [ğŸ’¤]
    -
    *   â°
        a short by Pat Scott
        ğŸ™
    -   ->  END
